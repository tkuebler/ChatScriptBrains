# this function is executed once for every new user chatting with Alice
outputmacro: Alice()  # you get Alice by default
$token = #DO_INTERJECTION_SPLITTING  | #DO_SUBSTITUTE_SYSTEM   | #DO_NUMBER_MERGE   | #DO_PROPERNAME_MERGE  | #DO_SPELLCHECK  | #DO_PARSE 

^addtopic(~introductions)
^mark(aimlkey)

$control_main = ~control
$control_post = ~XPOSTPROCESS
$userprompt = ^"%user: >"
$botprompt = ^"Alice: "

table: defaultbot (^name)
^createfact(^name defaultbot defaultbot)
DATA:
Alice

topic: ~control system ()

# on startup, do introduction
u: ( %input<%userfirstline) 
	gambit(~introductions)

u: () # main per-sentence processing

	^mark(aimlkey)
	
	$$currenttopic = %topic		# get the current topic at start of volley

	if ( %response == 0 ) {nofail(TOPIC ^rejoinder())}	# try for rejoinders. might generate an answer directly from what we are looking for.

	if (%length == 0 AND %response == 0 )
	{
		nofail(TOPIC ^gambit($$currenttopic))  # gambit current topic since no input (usually start of conversation)
	}

	if (%response == 0) { nofail(TOPIC ^respond($$currenttopic)) } # current topic tries to respond to his input

	if (%response == 0) # see if some other topic has keywords matching his input (given we have no response yet)
	{
		@8 = ^keywordtopics()	# get topics referred in input
		loop()
		{
			$$topic = first(@8subject)
			nofail(TOPIC ^respond($$topic))
			if (%response != 0) # stop when we find something to say
			{
				^end(RULE)	# we are done, this terminates the loop (not the rule)
			}
		}
	}	
	
	# if no topic reacts, go to the last ditch keywordless topic
	if (%response == 0) 
	{
		nofail(TOPIC ^respond(~keywordless))
	}

	# having no good response we know, now generate quibbles

	# now we need to find fake responses 
	if (%response == 0 AND %rand > 50)
	{
		nofail(TOPIC respond(~QUIBBLE_ALL))
	}

	# if we have rejoinders for what we said OR we asked a question, stop here
	if (%outputrejoinder OR %lastquestion) 
	{
		end(TOPIC)
	}


	if (%response == 0 AND ^marked($$currenttopic)) { nofail(TOPIC ^gambit($$currenttopic)) } # gambit current topic since keywords match current topic

	if (%response == 0) # gambit from ANY matching topic
	{
		@8 = ^keywordtopics()	# get topics referred in input
		loop()
		{
			$$topic = first(@8subject)
			nofail(TOPIC ^Gambit($$topic))	# gambit in best matching topic
			if (%response != 0) # stop when we find something
			{
				^end(RULE)
			}
		}
	}
	if (%response == 0){ nofail(TOPIC ^gambit($$currenttopic)) } # gambit from current topic even though no keywords matched
	
	if (%response == 0) 
	{ 
		@8 = ^GambitTopics()		# all topics with gambits (excluding system topics)
		loop()
		{
			$$topic = pick(@8subject)
			nofail(TOPIC ^Gambit($$topic))	# try a topic at random
			if (%response != 0)
			{
				^end(RULE)
			}
		}
	}
	
	if (%response == 0) 
	{
		 ^repeat() 
		 [I don't know what to say.][I'm at a loss for words.][hmnn..]
	}


# BELOW is logically equivalent to the above topic

topic: ~alternate_control system ()
u: () $$currenttopic = %topic		# get the current topic at start of volley
u: () ^rejoinder()	# try for rejoinders. might generate an answer directly from what we are looking for.
u: ( %response=0 %length=0) ^gambit($$currenttopic)  # gambit current topic since no input (usually start of conversation)
u: (%response=0) ^respond($$currenttopic) # current topic tries to respond to his input
u: (%response=0) 
		@8 = ^keywordtopics()	# get topics referred in input
		loop()
		{
			$$topic = first(@8subject)
			nofail(TOPIC Respond($$topic))
			if (%response != 0) # stop when we find something to say
			{
				^end(TOPIC)	# we are done, this terminates the loop (not the rule)
			}
		}
u: (%response=0  ^marked($$currenttopic))  ^gambit($$currenttopic)  # gambit current topic since keywords match current topic
u: (%response=0 )
		@8 = ^keywordtopics()	# get topics referred in input
		loop()
		{
			$$topic = first(@8subject)
			nofail(TOPIC ^Gambit($$topic))	# gambit in best matching topic
			if (%response != 0) # stop when we find something
			{
				^end(TOPIC)
			}
		}
u: (%response=0) ^gambit($$currenttopic)  # gambit from current topic even though no keywords matched
u: (%response=0)	
		@8 = ^GambitTopics()		# all topics with gambits (excluding system topics)
		loop()
		{
			$$topic = pick(@8)
			NOFAIL(TOPIC ^Gambit($$topic))	# try a topic at random
			if (%response != 0)
			{
				^end(TOPIC)
			}
		}
u: (%response=0)		
	^repeat() 
	I don't know what to say. 
	
topic: ~XPOSTPROCESS system () # gambits only - not allowed to write to  output - generates speech on windows -- for Linux you'd want to install Festival (or equivalent) and call that instead.

t: (^query(direct_v ? chatoutput ? -1 ? @9  ))  # get the sentences  (why is the object)
	loop()
	{
		$$tmp = ^last(@9subject) # note last output which is quoted string
		if (!%server AND %os == windows)
		{
			$$tmp = join(talk  " "  $$tmp )
			popen($$tmp null)
		}
	}
